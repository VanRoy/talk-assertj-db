plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.4'
    id 'org.asciidoctor.jvm.revealjs' version '4.0.4'
}

version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    ruby {
        gems()
    }
}

dependencies {

    implementation(platform("org.springframework.boot:spring-boot-dependencies:3.4.4"))
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')

    compileOnly('org.projectlombok:lombok:')
    annotationProcessor('org.projectlombok:lombok:1.18.36')

    runtimeOnly('com.h2database:h2') {
        version { strictly("1.4.200") }
    }

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.junit.jupiter:junit-jupiter-engine')
    testImplementation('org.assertj:assertj-db:3.0.0')
    testImplementation('org.dbunit:dbunit:2.7.3')
    testImplementation('com.github.springtestdbunit:spring-test-dbunit:1.3.0')
}

bootJar {
    enabled = false
}

test {
    useJUnitPlatform()
}

revealjs {
    version = '5.2.0'

    templateGitHub {
        organisation = 'hakimel'
        repository = 'reveal.js'
        tag = '5.2.0'
    }
}

asciidoctorRevealJs {
    sourceDir file("src/docs/asciidoc")

    baseDirFollowsSourceFile()

    resources {
        from("${sourceDir}/images") {
            include '**'
            into 'images'
        }
        from("${sourceDir}/css") {
            include '**'
            into 'css'
        }
        from("${sourceDir}/fonts") {
            include '**'
            into 'css/fonts'
        }
    }

    attributes 'build-gradle': file('build.gradle'),
            'endpoint-url': 'http://example.org',
            'source-highlighter': 'highlight.js',
            'highlightjs-theme': 'https://cdnjs.cloudflare.com/ajax/libs/highlight.js/10.7.3/styles/vs2015.min.css',
            'imagesdir': './images',
            'toc': 'left',
            'icons': 'font',
            'setanchors': '',
            'idprefix': 'slide-',
            'idseparator': '-',
            'docinfo': 'shared',
            'revealjs_customtheme': 'css/devoxx.css',
            'revealjs_transition': 'linear',
            'revealjs_history': 'true',
            'revealjs_slideNumber': 'true'
}

// alias
task asciidoctor(dependsOn: asciidoctorRevealJs)
