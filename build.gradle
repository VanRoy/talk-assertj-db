plugins {
    id 'java'
    id 'org.asciidoctor.jvm.gems' version '3.1.0'
    id 'org.asciidoctor.jvm.revealjs' version '3.1.0'
    id 'org.springframework.boot' version '2.2.5.RELEASE'
}

version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
    ruby {
        gems()
    }
}

dependencies {

    implementation(platform("org.springframework.boot:spring-boot-dependencies:2.2.5.RELEASE"))
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')

    compileOnly('org.projectlombok:lombok:1.18.2')
    annotationProcessor('org.projectlombok:lombok:1.18.2')

    runtimeOnly('com.h2database:h2')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.junit.jupiter:junit-jupiter-engine')
    testImplementation('org.assertj:assertj-db:1.3.0')
    testImplementation('dbunit:dbunit:2.2')
    testImplementation('com.github.springtestdbunit:spring-test-dbunit:1.3.0')
}

bootJar {
    enabled = false
}

test {
    useJUnitPlatform()
}

revealjs {
    version = '4.0.1'

    templateGitHub {
        organisation = 'hakimel'
        repository = 'reveal.js'
        tag = '3.9.2'
    }
}

asciidoctorRevealJs {
    sourceDir file("src/docs/asciidoc")

    baseDirFollowsSourceFile()

    resources {
        from("${sourceDir}/images") {
            include '**'
            into 'images'
        }
        from("${sourceDir}/css") {
            include '**'
            into 'css'
        }
    }

    attributes 'build-gradle': file('build.gradle'),
            'endpoint-url': 'http://example.org',
            'source-highlighter': 'coderay',
            'coderay-css': 'style',
            'imagesdir': './images',
            'toc': 'left',
            'icons': 'font',
            'setanchors': '',
            'idprefix': 'slide-',
            'idseparator': '-',
            'docinfo': 'shared',
            'revealjs_customtheme': 'css/pikestreet.css',
            'revealjs_transition': 'linear',
            'revealjs_history': 'true',
            'revealjs_slideNumber': 'true'
}

// alias
task asciidoctor(dependsOn: asciidoctorRevealJs)
